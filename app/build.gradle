apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def versionMajor = 3
def versionMinor = 0
def versionBuild = rootProject.ext.buildNumber.toInteger()

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    flavorDimensions "twitternurtelekom"
    defaultConfig {
        applicationId "com.twitternurtelekom"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        versionCode versionMajor * 1000000 + versionMinor * 100000 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionBuild}"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            if (System.getenv()["CI"]) {
                storeFile file(System.getenv()["KEYSTORE_PATH"])
                storePassword System.getenv()["KEYSTORE_PASSWORD"]
                keyAlias System.getenv()["KEY_ALIAS"]
                keyPassword System.getenv()["KEY_PASSWORD"]
            } else {
                def keystorePropertiesFile = rootProject.file("keystore.properties")
                if (keystorePropertiesFile.exists()) {
                    def keystoreProperties = new Properties()
                    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                    storeFile file(keystoreProperties['storeFile'])
                    storePassword keystoreProperties['storePassword']
                    keyAlias keystoreProperties['keyAlias']
                    keyPassword keystoreProperties['keyPassword']
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
        prod {
            dimension = "twitternurtelekom"
        }
        dev {
            applicationIdSuffix ".dev"
            dimension = "twitternurtelekom"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support:multidex:1.0.2'
    testImplementation 'junit:junit:4.12'

    //support libs
    implementation "com.android.support:support-vector-drawable:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Architecture components
    implementation "android.arch.lifecycle:extensions:$architectureVersion"
    implementation "android.arch.persistence.room:runtime:$room"
    implementation "android.arch.paging:runtime:1.0.0-alpha5"
    kapt "android.arch.lifecycle:compiler:$architectureVersion"
    kapt "android.arch.persistence.room:compiler:$room"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.21.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.21"

    //rx kotlin
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'

    implementation 'org.parceler:parceler-api:1.1.9'
    kapt 'org.parceler:parceler:1.1.9'

    //other
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'com.facebook.fresco:fresco:1.7.1'
    implementation 'id.zelory:compressor:2.1.0'
    implementation 'com.koushikdutta.ion:ion:2.2.1'


    // dagger
    implementation "com.google.dagger:dagger-android-support:$dagger"
    implementation "com.google.dagger:dagger:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
}
